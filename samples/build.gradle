apply plugin: 'android'

dependencies {
    compile project(":core")
    compile project(":ui")
    compile "com.android.support:support-v4:+"
    compile 'com.jakewharton:butterknife:4.0.+'
}

loadProperties("signing.properties")

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    signingConfigs {
        release {
            storeFile file("mcxiaoke.keystore")
            storePassword project.ext.get("storePassword")
            keyAlias project.ext.get("keyAlias")
            keyPassword project.ext.get("keyPassword")
        }
    }

    buildTypes {
        release {
            zipAlign true
            signingConfig signingConfigs.release
        }

        beta {
            signingConfig signingConfigs.release
            versionNameSuffix "-beta"
            debuggable true
        }

        debug {
            packageNameSuffix null
            versionNameSuffix "-dev"
        }
    }

    lintOptions {
        quiet true
        abortOnError false
    }

}

def loadProperties(String fileName) {
    def config = new Properties()
    def propFile = file(fileName)
    System.err.println("load property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("property " + property.key + ":" + property.value)
            project.ext[property.key] = property.value;
        }
    }
}

//apply from: 'https://raw.github.com/mcxiaoke/gradle-mvn-push/master/gradle-mvn-push.gradle'

